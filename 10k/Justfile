# List recipes.
help:
    @just -l -u

qb := 'quickbench'
#qb := 'quickbench -N2 -n2 -p3'

#time := 'time'
time := 'gtime'

tackler := 'tackler --config tackler.toml --input.fs.ext journal'

# Run the benchmarks with quickbench.
bench:
    {{ qb }} \
    'ledger  -f files1/10k.journal bal' \
    'hledger -f files1/10k.journal bal' \
    '{{ tackler }} --input.fs.dir files1' \
    '{{ tackler }} --input.fs.dir files28'

# Run the benchmarks with (g)time.
bench2:
    {{ time }} ledger  -f files1/10k.journal bal >/dev/null; echo
    {{ time }} hledger -f files1/10k.journal bal >/dev/null; echo
    {{ time }} {{ tackler }} --input.fs.dir files1  >/dev/null; echo
    {{ time }} {{ tackler }} --input.fs.dir files28 >/dev/null; echo

# Save the outputs generated by the benchmark commands, for sanity checking.
bench-outputs:
    mkdir -p outputs
    ledger  -f files1/10k.journal bal    >outputs/balances-ledger.txt
    hledger -f files1/10k.journal bal    >outputs/balances-hledger.txt
    {{ tackler }} --input.fs.dir files1  >outputs/balances-tackler-files1.txt
    {{ tackler }} --input.fs.dir files28 >outputs/balances-tackler-files28.txt

# Run benchmarks on a simpler journal, with quickbench.
simple:
    {{ qb }} \
    'ledger  -f simple-files1/10k-simple.journal bal' \
    'hledger -f simple-files1/10k-simple.journal bal' \
    '{{ tackler }} --input.fs.dir simple-files1' \

# Run benchmarks on a simpler journal, with (g)time.
simple2:
    {{ time }} ledger  -f simple-files1/10k-simple.journal bal >/dev/null; echo
    {{ time }} hledger -f simple-files1/10k-simple.journal bal >/dev/null; echo
    {{ time }} {{ tackler }} --input.fs.dir simple-files1 >/dev/null; echo

# Save the outputs generated by the simple benchmark commands, for sanity checking.
simple-outputs:
    mkdir -p outputs
    ledger  -f simple-files1/10k-simple.journal bal >outputs/simple-balances-ledger.txt
    hledger -f simple-files1/10k-simple.journal bal >outputs/simple-balances-hledger.txt
    {{ tackler }} --input.fs.dir simple-files1      >outputs/simple-balances-tackler.txt

# Regenerate the 28 yearly files from the main journal. (May need update.)
_files28:
    mkdir -p files28
    for y in `seq 2000 2027`; do echo files28/10k-$y.journal; hledger -f files1/10k.journal print date:$y >files28/10k-$y.journal; done

# Regenerate the 329 monthly files (+ 7 empties) from the main journal. (May need update.)
_files329:
    mkdir -p files329
    for y in `seq 2000 2027`; do for m in `seq 1 12`; do echo files329/10k-$y`printf "%02d" $m`.journal; hledger -f files1/10k.journal print date:$y/$m >files329/10k-$y`printf "%02d" $m`.journal; done; done
